import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.agilesolutions.gateway'
version = '1.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

}

configurations {
    standalone
}


ext {
    commit = commit()
    branchName = branchName()
    set('springCloudVersion', "2024.0.0")
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}


dependencies {
    //implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    // set up gateway as an OAuth2 client to extract the OAuth2 access token from the currently
    // authenticated user and adds it as a Bearer token to the downstream request on downstream service calls. This allows the downstream services to validate the token and extract user authorities for authorization decisions.
    //implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
}

test {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        // Show standard out/err from the tests
        showStandardStreams = true

        // Events to log
        events "passed", "skipped", "failed"

        // Log exception details
        exceptionFormat "full" // options: short, full

        // Granularity of logging
        displayGranularity = 2 // show nested test classes/methods nicely
    }

    // Optional: print summary after execution
    afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
            println "\n--------------------------------------------------"
            println "Test Results: ${result.resultType}"
            println "Tests run: ${result.testCount}, Passed: ${result.successfulTestCount}, Failed: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}"
            println "--------------------------------------------------"
        }
    }
}

String commit() {
    return 'git rev-parse HEAD'.execute().text.trim()
}

String branchName() {
    return hasProperty('branch.name') ? property('branch.name') : 'git branch --show-current'.execute().text.trim()
}

task generateHelmChart(type: Copy) {
    inputs.dir('helm')
    inputs.file('build.gradle')
    from('helm') {
        include 'gateway/**'
        filter(ReplaceTokens, tokens: [appVersion: version])
    }
    into('build')

}

bootJar {

    archiveFileName = './application.jar'

    manifest{
        attributes(
                'Built-by'          : System.properties['user.name'],
                'Build-Timestamp'   : java.time.Instant.now(),
                'Build-Revision'    : commit,
                'Build-Branch'      : branchName,
                'Created-by'        : "Gradle ${gradle.gradleVersion}",
                'Build-JDK'         : "${System.properties['java.version']} ${System.properties['java.vendor']} ${System.properties['java.vm.version']}",
                'Build-OS'          : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )

    }

}

task dockerBuild(type: Exec) {
    commandLine 'docker', 'build', '-t', "agilesolutions/gateway:${version}", '.'
}

task dockerPush(type: Exec) {
    commandLine 'docker', 'push', "agilesolutions/gateway:${version}"
}

task release(type: GradleBuild) {
    tasks = ['clean', 'build', 'dockerBuild']
}

test.dependsOn generateHelmChart
compileTestJava.dependsOn generateHelmChart
generateHelmChart.dependsOn bootJar, jar